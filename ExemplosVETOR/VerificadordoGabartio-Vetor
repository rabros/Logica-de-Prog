algoritmo "CORRIGE PROVAS"
var
    nomes: vetor [1..3] de caractere
    gabarito: vetor[1..5] de caractere
    nota: vetor [1..3] de real
    prova: vetor[1..5] de caractere
    i: inteiro
    s, m: real

Procedimento CadastroGabarito()
var
    c: inteiro
inicio
    para c <- 1 ate 5 faca
        EscrevaL("CADASTRO DO GABARITO")
        EscrevaL("QuestÃ£o ", c, ":")
        leia(gabarito[c])
        LimpaTela
    FimPara
FimProcedimento 
Funcao CadastraProva(): Real
var
    c: inteiro
    notaFinal: real
inicio
    notaFinal <- 0
    para c <- 1 ate 5 faca
        EscrevaL("RESPOSTA DO ALUNO")
        EscrevaL("Questao: ", c)
        leia (prova[c])
        se (Maiusc(prova[c]) = Maiusc(gabarito[c])) entao
           notaFinal <- notaFinal + 2
        FimSe
    FimPara
    retorne notaFinal
FimFuncao

inicio
    CadastroGabarito()
    Para i <- 1 ate 3 faca
        EscrevaL("Digite o nome: ")
        leia (nomes[i])
        nota[i] <- CadastraProva()
        s <- s + nota[i]
        Limpatela
    FimPara

    LimpaTela
    Para i <- 1 ate 3 faca
        EscrevaL("Aluno: ", nomes[i], " | Nota: ", nota[i])
    FimPara
    m <- s / 3
    Escreva ("Media da turma ", m:4:2)
fimalgoritmo
